CREATE DATABASE QLKSSS
GO
USE QLKSSS
GO

-- 1. TẠO CÁC BẢNG
CREATE TABLE KHACH (
    MAKH INT IDENTITY(1,1) NOT NULL,
    TENKH NVARCHAR(50) NOT NULL,
    GIOITINH NVARCHAR(4),
    SDT VARCHAR(15),
    MADINHDANH NVARCHAR(20) NOT NULL,
    LOAIGIAYTO NVARCHAR(20) NOT NULL,
    QUOCTICH NVARCHAR(30),
    CONSTRAINT PK_KHACH PRIMARY KEY(MAKH),
    CONSTRAINT CHECK_GT_KH CHECK (GIOITINH IN (N'Nam', N'Nữ')),
    CONSTRAINT UQ_MADINHDANH_KH UNIQUE (MADINHDANH),
    CONSTRAINT CHK_SDT_KH CHECK (SDT LIKE '0%' AND LEN(SDT) BETWEEN 10 AND 11)
)

CREATE TABLE LOAIPHONG (
    MALP INT IDENTITY(1,1) NOT NULL,
    TENLP NVARCHAR(50) NOT NULL,
    GIA MONEY NOT NULL,
    SONGUOITOIDA INT NOT NULL,
    CONSTRAINT PK_LOAIPHONG PRIMARY KEY (MALP),
    CONSTRAINT CHK_GIA_LOAIPHONG CHECK (GIA > 0),
    CONSTRAINT CHK_SONGUOITOIDA CHECK (SONGUOITOIDA BETWEEN 1 AND 4)
)

CREATE TABLE PHONG (
    MAPH INT IDENTITY(1,1) NOT NULL,
    SOPHONG VARCHAR(10) NOT NULL,
    MALP INT NOT NULL,
    TRANGTHAI NVARCHAR(20) NOT NULL DEFAULT N'Trống', -- Đã gộp Default vào đây
    CONSTRAINT PK_PHONG PRIMARY KEY (MAPH),
    CONSTRAINT UQ_SOPHONG UNIQUE (SOPHONG),
    CONSTRAINT FK_PHONG_LOAIPHONG FOREIGN KEY (MALP) REFERENCES LOAIPHONG(MALP),
    CONSTRAINT CHK_TRANGTHAI_PHONG CHECK (TRANGTHAI IN (N'Trống', N'Đã đặt', N'Đang ở'))
)

CREATE TABLE NHANVIEN (
    MANV INT IDENTITY(1,1) NOT NULL,
    TENNV NVARCHAR(50) NOT NULL,
    GIOITINH NVARCHAR(4) NOT NULL,
    SDT VARCHAR(15) NOT NULL,
    CCCD VARCHAR(12) NOT NULL,
    DIACHI NVARCHAR(100),
    CHUCVU NVARCHAR(30) NOT NULL,
    NGAYVAOLAM DATE NOT NULL,
    CONSTRAINT PK_NHANVIEN PRIMARY KEY (MANV),
    CONSTRAINT CHK_GIOITINH_NV CHECK (GIOITINH IN (N'Nam', N'Nữ')),
    CONSTRAINT UQ_CCCD_NV UNIQUE (CCCD),
    CONSTRAINT CHK_NGAYVAOLAM CHECK (NGAYVAOLAM <= GETDATE()),
    CONSTRAINT CHK_SDT_NV CHECK (SDT LIKE '0%' AND LEN(SDT) BETWEEN 10 AND 11)
)

CREATE TABLE TAIKHOAN (
    MATK INT IDENTITY(1,1) NOT NULL,
    MANV INT NOT NULL, -- Sửa: Nên là NOT NULL
    TENTK VARCHAR(50) NOT NULL, -- Sửa: VARCHAR(MAX) cho tên TK là quá thừa
    MATKHAU VARCHAR(100) NOT NULL,
    VAITRO NVARCHAR(20) NOT NULL,
    NGAYTAO DATE NOT NULL DEFAULT GETDATE(),
    TRANGTHAI NVARCHAR(20) NOT NULL DEFAULT N'Hoạt động',
    CONSTRAINT PK_TAIKHOAN PRIMARY KEY (MATK),
    CONSTRAINT FK_TAIKHOAN_NV FOREIGN KEY (MANV) REFERENCES NHANVIEN(MANV),
    CONSTRAINT UNI_TAIKHOANNV UNIQUE (MANV), -- Đã gộp Unique vào đây
    CONSTRAINT CHK_VAITRO CHECK (VAITRO IN (N'Quản trị viên', N'Nhân viên')),
    CONSTRAINT CHK_TRANGTHAI_TK CHECK (TRANGTHAI IN (N'Hoạt động', N'Khóa'))
)

CREATE TABLE DATPHONG (
    MADATPH INT IDENTITY(1,1) NOT NULL,
    MAKH INT NOT NULL,
    MAPH INT NOT NULL,
    NGAYDAT DATE NOT NULL DEFAULT GETDATE(),
    NGAYNHAN DATE NOT NULL,
    NGAYTRA DATE,
    CONSTRAINT PK_DATPHONG PRIMARY KEY (MADATPH),
    CONSTRAINT FK_DATPH_KH FOREIGN KEY (MAKH) REFERENCES KHACH(MAKH),
    CONSTRAINT FK_DATPH_PHONG FOREIGN KEY (MAPH) REFERENCES PHONG(MAPH),
    CONSTRAINT CHK_NGAYDAT_NHAN CHECK (NGAYNHAN >= NGAYDAT),
    CONSTRAINT CHK_NGAYTRA_NHAN CHECK (NGAYTRA IS NULL OR NGAYTRA >= NGAYNHAN)
)

CREATE TABLE DICHVU (
    MADV INT IDENTITY(1,1) NOT NULL,
    TENDV NVARCHAR(50) NOT NULL,
    DONGIA MONEY NOT NULL,
    CONSTRAINT PK_DICHVU PRIMARY KEY (MADV),
    CONSTRAINT CHK_DONGIA_DV CHECK (DONGIA > 0)
)

CREATE TABLE SUDUNGDV (
    MASDDV INT IDENTITY(1, 1) NOT NULL,
    MADATPH INT NOT NULL,
    MADV INT NOT NULL,
    MANV INT,
    SOLUONG INT NOT NULL,
    GHICHU NVARCHAR(MAX), -- Đã gộp cột GHICHU vào đây
    CONSTRAINT PK_SDDV PRIMARY KEY (MASDDV),
    CONSTRAINT FK_SDDV_DATPH FOREIGN KEY (MADATPH) REFERENCES DATPHONG(MADATPH),
    CONSTRAINT FK_SDDV_DV FOREIGN KEY (MADV) REFERENCES DICHVU(MADV),
    CONSTRAINT FK_SDDV_NV FOREIGN KEY (MANV) REFERENCES NHANVIEN(MANV),
    CONSTRAINT CHK_SOLUONG_DV CHECK (SOLUONG > 0)
)

CREATE TABLE HOADON (
    MAHD INT IDENTITY(1,1) NOT NULL,
    MADATPH INT NOT NULL,
    NGAYLAP DATE NOT NULL DEFAULT GETDATE(),
    CONSTRAINT PK_HOADON PRIMARY KEY (MAHD),
    CONSTRAINT FK_HD_DATPH FOREIGN KEY (MADATPH) REFERENCES DATPHONG(MADATPH),
    CONSTRAINT UQ_HD UNIQUE (MADATPH)
);

CREATE TABLE CHITIETHD (
    MAHD INT NOT NULL,
    MADV INT NOT NULL,
    SOLUONG INT NOT NULL,
    GIADV MONEY NOT NULL,
    CONSTRAINT PK_CTHD PRIMARY KEY (MAHD, MADV),
    CONSTRAINT FK_CTHD_HD FOREIGN KEY (MAHD) REFERENCES HOADON(MAHD),
    CONSTRAINT FK_CTHD_DV FOREIGN KEY (MADV) REFERENCES DICHVU(MADV),
    CONSTRAINT CHK_SOLUONG_HD CHECK (SOLUONG > 0),
    CONSTRAINT CHK_GIADV CHECK (GIADV >= 0)
);
GO

-- 2. CÁC PROCEDURE VÀ TRIGGER

-- Proc Thêm Khách
CREATE PROCEDURE sp_ThemKhach
    @TenKH NVARCHAR(50),
    @GioiTinh NVARCHAR(4),
    @SDT VARCHAR(15),
    @MaDinhDanh NVARCHAR(20),
    @LoaiGiayTo NVARCHAR(20),
    @QuocTich NVARCHAR(30)
AS
BEGIN
    IF EXISTS (SELECT * FROM KHACH WHERE MADINHDANH = @MaDinhDanh)
        PRINT N'Khách hàng đã tồn tại!';
    ELSE
    BEGIN
        INSERT INTO KHACH (TENKH, GIOITINH, SDT, MADINHDANH, LOAIGIAYTO, QUOCTICH)
        VALUES (@TenKH, @GioiTinh, @SDT, @MaDinhDanh, @LoaiGiayTo, @QuocTich);
        PRINT N'Thêm khách hàng thành công!';
    END
END;
GO

-- Trigger: Kiểm tra trùng lịch (QUAN TRỌNG: Đã sửa lại logic trùng lặp)
CREATE TRIGGER trg_KiemTraTrungLich
ON DATPHONG
INSTEAD OF INSERT
AS
BEGIN
    -- Logic trùng: Ngày Nhận của đơn mới < Ngày Trả của đơn cũ 
    -- VÀ Ngày Trả của đơn mới > Ngày Nhận của đơn cũ
    IF EXISTS (
        SELECT 1
        FROM DATPHONG DP
        JOIN inserted i ON DP.MAPH = i.MAPH
        WHERE DP.NGAYTRA IS NOT NULL -- Chỉ so sánh với các đơn chưa hủy/đã chốt ngày
          AND (i.NGAYNHAN < DP.NGAYTRA AND i.NGAYTRA > DP.NGAYNHAN)
    )
    BEGIN
        RAISERROR (N'Lỗi: Phòng đã được đặt trùng lịch trong khoảng thời gian này!', 16, 1);
        ROLLBACK TRANSACTION;
        RETURN;
    END

    -- Nếu không trùng thì Insert
    INSERT INTO DATPHONG (MAKH, MAPH, NGAYDAT, NGAYNHAN, NGAYTRA)
    SELECT MAKH, MAPH, NGAYDAT, NGAYNHAN, NGAYTRA FROM inserted;
END;
GO

-- Trigger: Cập nhật trạng thái phòng khi đặt

CREATE TRIGGER trg_CapNhatTrangThaiPhongKhiDat
ON DATPHONG
AFTER INSERT
AS
BEGIN
    -- Nếu ngày đặt = ngày nhận → Đang ở
    UPDATE PHONG
    SET TRANGTHAI = N'Đang ở'
    WHERE MAPH IN (
        SELECT MAPH FROM inserted
        WHERE CAST(NGAYDAT AS DATE) = CAST(NGAYNHAN AS DATE)
    );

    -- Nếu ngày đặt < ngày nhận → Đã đặt
    UPDATE PHONG
    SET TRANGTHAI = N'Đã đặt'
    WHERE MAPH IN (
        SELECT MAPH FROM inserted
        WHERE CAST(NGAYDAT AS DATE) < CAST(NGAYNHAN AS DATE)
    );
END;
GO

-- Trigger: Cập nhật trạng thái phòng khi trả
CREATE TRIGGER trg_CapnhatPhongKhiTra 
ON DATPHONG
AFTER UPDATE
AS
BEGIN
    UPDATE PHONG
    SET TRANGTHAI = N'Trống'
    WHERE MAPH IN (
        SELECT i.MAPH
        FROM inserted i
        JOIN deleted d ON d.MADATPH = i.MADATPH
        WHERE d.NGAYTRA IS NULL AND i.NGAYTRA IS NOT NULL
    );
END
GO


  
-- Trigger: Tạo hóa đơn tự động khi trả phòng
CREATE TRIGGER trg_TaoHoaDonSauKhiTraPhong
ON DATPHONG
AFTER UPDATE
AS
BEGIN
    INSERT INTO HOADON (MADATPH, NGAYLAP)
    SELECT i.MADATPH, GETDATE()
    FROM inserted i
    JOIN deleted d ON d.MADATPH = i.MADATPH
    WHERE d.NGAYTRA IS NULL -- Trước đó chưa trả
      AND i.NGAYTRA IS NOT NULL -- Giờ đã trả
      AND NOT EXISTS (SELECT 1 FROM HOADON WHERE MADATPH = i.MADATPH);
END;
GO

-- Hàm: Tính tiền phòng (Sửa lỗi 0 ngày)
CREATE FUNCTION fn_TongTienPhong(@MaDatPh INT)
RETURNS MONEY
AS
BEGIN
    DECLARE @Tien MONEY;
    
    SELECT @Tien = 
        CASE 
            WHEN DATEDIFF(DAY, NGAYNHAN, NGAYTRA) = 0 THEN LP.GIA -- Nếu ở trong ngày thì tính 1 ngày
            ELSE DATEDIFF(DAY, NGAYNHAN, NGAYTRA) * LP.GIA
        END
    FROM DATPHONG DP
    JOIN PHONG P ON DP.MAPH = P.MAPH
    JOIN LOAIPHONG LP ON P.MALP = LP.MALP
    WHERE DP.MADATPH = @MaDatPh;
    
    RETURN ISNULL(@Tien, 0);
END;
GO

-- Hàm: Tính tiền dịch vụ
CREATE FUNCTION fn_TongTienDichVu(@MaDatPh INT)
RETURNS MONEY
AS
BEGIN
    DECLARE @TongTien MONEY = 0;

    SELECT @TongTien = SUM(SOLUONG * DONGIA)
    FROM SUDUNGDV SD
    JOIN DICHVU DV ON SD.MADV = DV.MADV
    WHERE SD.MADATPH = @MaDatPh;
    
    RETURN ISNULL(@TongTien, 0); 
END;
GO

-- Proc: Thêm dịch vụ vào phòng
CREATE PROC sp_ThemDichVuVaoPhong
    @MADATPH INT,
    @MADV INT,
    @MANV INT,
    @SOLUONG INT
AS
BEGIN 
    IF @SOLUONG <= 0
    BEGIN 
        PRINT N'SỐ LƯỢNG PHẢI LỚN HƠN 0'
        RETURN
    END
    IF NOT EXISTS (SELECT 1 FROM DATPHONG WHERE MADATPH = @MADATPH)
    BEGIN
        PRINT N'MÃ ĐẶT PHÒNG KHÔNG TỒN TẠI'
        RETURN
    END
    IF NOT EXISTS (SELECT 1 FROM DICHVU WHERE MADV = @MADV)
    BEGIN
        PRINT N'MÃ DỊCH VỤ KHÔNG TỒN TẠI'
        RETURN
    END
    
    INSERT INTO SUDUNGDV (MADATPH, MADV, MANV, SOLUONG) 
    VALUES (@MADATPH, @MADV, @MANV, @SOLUONG)
    
    PRINT N'DỊCH VỤ ĐÃ ĐẶT THÀNH CÔNG!'
END
GO

-- 3. CÁC THỦ TỤC THỐNG KÊ (ĐÃ SỬA LOGIC)
-- Lưu ý: Không dùng bảng CHITIETHD vì nó rỗng, dùng trực tiếp Hàm tính tiền

CREATE PROCEDURE sp_TongDoanhThuKhachSan
AS
BEGIN
    DECLARE @TongTien MONEY;

    -- Tính tổng tất cả hóa đơn dựa trên (Tiền phòng + Tiền DV)
    SELECT @TongTien = SUM(dbo.fn_TongTienPhong(MADATPH) + dbo.fn_TongTienDichVu(MADATPH))
    FROM HOADON;

    PRINT N'Tổng doanh thu của khách sạn là: ' + FORMAT(ISNULL(@TongTien, 0), '#,##0');
END;
GO

CREATE PROCEDURE sp_DoanhThuTheoNgay
AS
BEGIN
    SET NOCOUNT ON;
    SELECT CONVERT(date, NGAYLAP) AS NGAY,
           SUM(dbo.fn_TongTienPhong(MADATPH) + dbo.fn_TongTienDichVu(MADATPH)) AS DOANHTHU
    FROM HOADON
    GROUP BY CONVERT(date, NGAYLAP)
    ORDER BY NGAY;
END;
GO

CREATE PROCEDURE sp_DoanhThuTheoThang
AS
BEGIN
    SELECT YEAR(NGAYLAP) AS NAM,
           MONTH(NGAYLAP) AS THANG,
           SUM(dbo.fn_TongTienPhong(MADATPH) + dbo.fn_TongTienDichVu(MADATPH)) AS DOANHTHU
    FROM HOADON
    GROUP BY YEAR(NGAYLAP), MONTH(NGAYLAP)
    ORDER BY NAM, THANG;
END;
GO

CREATE PROCEDURE sp_DoanhThuTheoNam
AS
BEGIN
    SELECT YEAR(NGAYLAP) AS NAM,
           SUM(dbo.fn_TongTienPhong(MADATPH) + dbo.fn_TongTienDichVu(MADATPH)) AS DOANHTHU
    FROM HOADON
    GROUP BY YEAR(NGAYLAP)
    ORDER BY NAM;
END;
GO

CREATE PROCEDURE sp_TanSuatSuDungDichVu
AS
BEGIN
    SELECT dv.MADV,
           dv.TENDV,
           COUNT(sddv.MASDDV) AS SOLUOT, -- Số lần gọi dịch vụ
           SUM(sddv.SOLUONG) AS TONG_SOLUONG_DA_BAN
    FROM DICHVU dv
    LEFT JOIN SUDUNGDV sddv ON sddv.MADV = dv.MADV 
    GROUP BY dv.MADV, dv.TENDV
    ORDER BY SOLUOT DESC;
END;
GO

ALTER TABLE SUDUNGDV
ADD TRANGTHAI NVARCHAR(20) NOT NULL DEFAULT N'Đã đặt';


ALTER TABLE SUDUNGDV
ADD CONSTRAINT CK_SUDUNGDV_TrangThai
CHECK (TRANGTHAI IN (N'Đã đặt', N'Đang thực hiện', N'Đã hoàn thành'));

-- 4. INSERT DỮ LIỆU MẪU
INSERT INTO LOAIPHONG (TENLP, GIA, SONGUOITOIDA) VALUES (N'Phòng Đơn (Standard)', 500000, 1);
INSERT INTO LOAIPHONG (TENLP, GIA, SONGUOITOIDA) VALUES (N'Phòng Đôi (Deluxe)', 1200000, 2);
INSERT INTO LOAIPHONG (TENLP, GIA, SONGUOITOIDA) VALUES (N'Phòng VIP (Suite)', 3000000, 4);

INSERT INTO PHONG (SOPHONG, MALP, TRANGTHAI) VALUES ('101', 1, N'Trống');
INSERT INTO PHONG (SOPHONG, MALP, TRANGTHAI) VALUES ('102', 1, N'Trống');
INSERT INTO PHONG (SOPHONG, MALP, TRANGTHAI) VALUES ('201', 2, N'Trống');
INSERT INTO PHONG (SOPHONG, MALP, TRANGTHAI) VALUES ('202', 2, N'Đang ở');
INSERT INTO PHONG (SOPHONG, MALP, TRANGTHAI) VALUES ('301', 3, N'Trống');


INSERT INTO NHANVIEN (TENNV, GIOITINH, SDT, CCCD, DIACHI, CHUCVU, NGAYVAOLAM)
VALUES (N'Nguyễn Văn Quản Lý', N'Nam', '0901234567', '079090123456', N'Quận 1, TP.HCM', N'Quản lý', '2023-01-01');

INSERT INTO NHANVIEN (TENNV, GIOITINH, SDT, CCCD, DIACHI, CHUCVU, NGAYVAOLAM)
VALUES (N'Trần Thị Lễ Tân', N'Nữ', '0909876543', '079090654321', N'Quận 3, TP.HCM', N'Lễ tân', '2023-06-15');

-- Insert Tài khoản (Sửa lỗi thiếu TENTK)
INSERT INTO TAIKHOAN (MANV, TENTK, MATKHAU, VAITRO, TRANGTHAI)
VALUES (1, 'admin', 'admin@123', N'Quản trị viên', N'Hoạt động');

INSERT INTO TAIKHOAN (MANV, TENTK, MATKHAU, VAITRO, TRANGTHAI)
VALUES (2, 'staff', 'staff@123', N'Nhân viên', N'Hoạt động');

INSERT INTO DICHVU (TENDV, DONGIA) VALUES (N'Nước suối Aquafina', 10000);
INSERT INTO DICHVU (TENDV, DONGIA) VALUES (N'Bia Heineken', 25000);
INSERT INTO DICHVU (TENDV, DONGIA) VALUES (N'Mì ly Modern', 15000);
INSERT INTO DICHVU (TENDV, DONGIA) VALUES (N'Giặt ủi (kg)', 30000);
INSERT INTO DICHVU (TENDV, DONGIA) VALUES (N'Buffet Sáng', 150000);
INSERT INTO DICHVU (TENDV, DONGIA) VALUES (N'Thuê xe máy (ngày)', 200000);
GO

INSERT INTO KHACH (TENKH, GIOITINH, SDT, MADINHDANH, LOAIGIAYTO, QUOCTICH)
VALUES 
(N'Lê Văn Hoàng', N'Nam', '0912345678', '123456789', N'CCCD', N'Việt Nam'),
(N'Nguyễn Thị Hằng', N'Nữ', '0987654321', '987654321', N'CCCD', N'Việt Nam');

-- Khách 1 ở phòng 202 (phòng đang ở sẵn)
INSERT INTO DATPHONG (MAKH, MAPH, NGAYDAT, NGAYNHAN, NGAYTRA)
VALUES (12, 4, '2025-01-10', '2025-01-10', NULL);

-- Khách 2 đặt phòng 201
INSERT INTO DATPHONG (MAKH, MAPH, NGAYDAT, NGAYNHAN, NGAYTRA)
VALUES (13, 3, '2025-01-11', '2025-01-12', NULL);

-- Đơn 1 sử dụng dịch vụ
INSERT INTO SUDUNGDV (MADATPH, MADV, MANV, SOLUONG, TRANGTHAI)
VALUES (1, 1, 2, 2, N'Đã đặt'); -- 2 chai nước

INSERT INTO SUDUNGDV (MADATPH, MADV, MANV, SOLUONG, TRANGTHAI)
VALUES (1, 5, 2, 1, N'Đã đặt'); -- Buffet sáng

-- Đơn 2 sử dụng dịch vụ
INSERT INTO SUDUNGDV (MADATPH, MADV, MANV, SOLUONG, TRANGTHAI)
VALUES (2, 2, 2, 3, N'Đang thực hiện'); -- 3 bia

INSERT INTO SUDUNGDV (MADATPH, MADV, MANV, SOLUONG, TRANGTHAI)
VALUES (2, 3, 2, 1, N'Đang thực hiện'); -- Mì ly

-- Khách 1 trả phòng
UPDATE DATPHONG 
SET NGAYTRA = '2025-01-11'
WHERE MADATPH = 12;

-- Khách 2 trả phòng
UPDATE DATPHONG 
SET NGAYTRA = '2025-01-13'
WHERE MADATPH = 13;

UPDATE DATPHONG
SET NGAYTRA = '2025-01-11'
WHERE MADATPH = 7;

UPDATE DATPHONG
SET NGAYTRA = '2025-01-12'
WHERE MADATPH = 8;

UPDATE DATPHONG
SET NGAYTRA = '2025-01-13'
WHERE MADATPH = 9;

SELECT * FROM HOADON

SELECT 
    HD.MAHD,
    HD.MADATPH,
    dbo.fn_TongTienPhong(HD.MADATPH) AS TienPhong,
    dbo.fn_TongTienDichVu(HD.MADATPH) AS TienDV,
    dbo.fn_TongTienPhong(HD.MADATPH) + dbo.fn_TongTienDichVu(HD.MADATPH) AS TongTien
FROM HOADON HD;




